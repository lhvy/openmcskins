import proguard.gradle.ProGuardTask

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.guardsquare:proguard-gradle:7.2.1'
        classpath 'com.guardsquare:proguard-base:7.2.1'
    }
}

plugins {
    id 'fabric-loom' version '0.12-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.mod_name
version = project.mod_version
group = project.maven_group

repositories {
    mavenCentral()
    maven { url 'https://maven.parchmentmc.org' }
    maven { url 'https://maven.shedaniel.me/' }
    maven { url 'https://maven.terraformersmc.com/' }
    maven { url 'https://jitpack.io/' }
    maven {
        url 'https://api.modrinth.com/maven'
        content { includeGroup "maven.modrinth" }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"

    mappings loom.layered() {
        mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
        parchment "org.parchmentmc.data:parchment-${project.minecraft_version}:${project.parchment_version}@zip"
    }

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    implementation "io.reactivex.rxjava3:rxjava:${project.rxjava_version}"
    shadow "io.reactivex.rxjava3:rxjava:${project.rxjava_version}"

    modImplementation("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
        exclude group: "net.fabricmc"
        exclude group: "net.fabricmc.fabric-api"
    }
    modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}") {
        exclude group: "net.fabricmc"
        exclude group: "net.fabricmc.fabric-api"
    }

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}+${project.minecraft_version}"
    modImplementation "maven.modrinth:cem:${project.cem_version}"
    modImplementation "com.github.Chocohead:Fabric-ASM:${project.fabric_asm_version}"
}

// Using remote repo as mod site if project.mod_url is missing

def url

if (project.hasProperty("mod_url")) url = project.mod_url else {
    final process = 'git ls-remote --get-url'.execute()
    process.waitFor()
    url = process.text.trim()
}

// Format fabric.mod.json

processResources {
    inputs.properties "version": project.version,
            "mod_name": project.mod_name,
            "mod_url": url

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "mod_name": project.mod_name,
                "mod_url": url
    }
}

final String VERSION_AND_NAME = "${project.archivesBaseName}-${project.version}"

// Create shadow jar, because ReactiveX is third-party library

project.configurations.implementation.setCanBeResolved(true)

shadowJar {
    configurations = [project.configurations.shadow]
    archiveClassifier.set("dev-shadow")

    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
    from "NOTICE"

    dependencies {
        exclude '**/META-INF/proguard/**'
    }
    minimize()
}

remapJar {
    dependsOn(shadowJar)

    inputFile.set(shadowJar.archiveFile)
}

// Optimize using ProGuard because 3MB is too much

task proguard(type: ProGuardTask) {
    dependsOn(remapJar)

    configuration "${projectDir}/optimize.pro"
    injars "${buildDir}/libs/${VERSION_AND_NAME}.jar"
    outjars "${buildDir}/libs/${VERSION_AND_NAME}-optimized.jar"

    libraryjars(project.configurations.modImplementation.files)
    libraryjars(project.configurations.modApi.files)
}